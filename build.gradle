// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.31'

    ext {
        // global variables
        compile_sdk = 30
        min_sdk = 19
        build_tools_version = "30.0.0"
        application_id = "com.pj.playground"
        version_code = 1
        version_name = "1.0"
        coroutines_version = '1.4.3'

        // local variables(use def)
        def appcompat_version = '1.2.0'
        def constraint_layout_version = '2.0.0-rc1'
        def core_ktx_version = '1.3.1'
        def junit_version = '4.13'
        def androidx_test_version = '1.1.1'
        def espresso_version = '3.2.0'
        def material_version = '1.1.0-alpha05'
        def room_version = "2.2.6"

        libraries = [
                // Kotlin standard library
                "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",

                // Android UI & AppCompat
                "androidx.appcompat:appcompat:$appcompat_version",
                "androidx.constraintlayout:constraintlayout:$constraint_layout_version",
                "androidx.core:core-ktx:$core_ktx_version",
                "com.google.android.material:material:$material_version",
                "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version",
                "androidx.room:room-runtime:$room_version",
                "androidx.room:room-ktx:$room_version",

                // Java/Kotlin Library/Module
                project(':core')
        ]

        kaptLibraries = [
                "androidx.room:room-compiler:$room_version"
        ]

        testLibraries = [
                "junit:junit:$junit_version"
        ]

        androidTestLibraries = [
                "androidx.test.ext:junit:$androidx_test_version",
                "androidx.test.espresso:espresso-core:$espresso_version"
        ]

        kotlinModuleLibs = [
                "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",
                "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
        ]
    }

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
